WHITESPACE = _{ " " | "\t" }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
doc = { SOI ~ NEWLINE* ~ operation* ~ NEWLINE* ~ EOI }
operation = { mnemonic ~ operands ~ NEWLINE+ }
operands = {expr ~ ("," ~ term)? }
neg_term = { "-" ~ signless }
nonneg_term = { "+" ~ signless }
negativity = { neg_term | nonneg_term | signless }
signless = { abs_term | term }
abs_term = { "|" ~ (mul_quo | memory) ~ "|" }
expr = { negativity }
mul_quo = { ^"MQ" }
term = { mul_quo | memory }
memory = { ^"M" ~ address }
address = { "(" ~ address_num ~ ("," ~ slice)? ~ ")" }
address_num = @{ ASCII_DIGIT{1,4} }
slice_num = @{ ASCII_DIGIT{1,2} }
slice = { slice_num ~ ":" ~ slice_num }
mnemonic = { load | stor | jump | add | sub | mul | div | lsh | rsh }
load = { ^"load" }
stor = { ^"stor" }
jump = { ^"jump" }
add = { ^"add" }
sub = { ^"sub" }
mul = { ^"mul" }
div = { ^"div" }
lsh = { ^"lsh" }
rsh = { ^"rsh" }
